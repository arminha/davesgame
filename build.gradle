subprojects {

    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: 'jacoco'
    apply plugin: 'pmd'
    apply plugin: 'checkstyle'

    sourceCompatibility = 1.7

    /////////////////
    // java

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'com.google.code.findbugs:jsr305:2.0.3'
        compile 'com.google.guava:guava:15.0'

        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-core:1.9.5'
        testCompile 'org.hamcrest:hamcrest-library:1.3+'
    }

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }

    /////////////////
    // jacoco

    jacoco {
        toolVersion = '0.7.0.201403182114'
    }
    jacocoTestReport.dependsOn(test)

    /////////////////
    // findbugs

    findbugs {
        toolVersion = "2.0.3"
        ignoreFailures = true
    }

    if (!rootProject.hasProperty("findbugsReportXml") || findbugsReportXml != 'true') {
        tasks.withType(FindBugs) {
            reports {
                xml.enabled = false
                html.enabled = true
            }
        }
    }

    /////////////////
    // pmd

    pmd {
        ignoreFailures = true
        toolVersion = '5.0.5'
    }

    /////////////////
    // checkstyle

    checkstyle {
        ignoreFailures = true
        toolVersion = '5.7'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }
}

////////////////
// wrapper

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

